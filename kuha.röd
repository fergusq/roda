hae_kuva_ja_tee_koodi sana mones montako {
	hae_kuva sana mones
	x_koordinaatti := 'mones*500/montako'
	leveys := '500/montako'
	push "<image xlink:href='"..mones.."' preserveAspectRatio='none' x='"..x_koordinaatti.."' y='50' width='"..leveys.."' height='150' style='opacity:0.4;'/>"
}

/* tekee svg-kuvan */
tee_pilakuva nettikuvat ylä ala tiedosto {
	push "<?xml version='1.0' encoding='UTF-8'?>
	<svg
	xmlns='http://www.w3.org/2000/svg'
	xmlns:xlink='http://www.w3.org/1999/xlink' version='1.1'
	viewBox='0 0 500 244'>
	<style type=\"text/css\">@font-face{font-family:'imppa';src:url('Impact.ttf');}text{font-family:'serif';font-size:35px;fill:white;stroke:black;stroke-width:2px;text-anchor:middle}</style>
	<image xlink:href='kuha.jpg' x='0' y='0' width='500' height='244'/>
	"..nettikuvat.."
	<text x='50%' y='14%'>"..ylä.."</text>
	<text x='50%' y='98%'>"..ala.."</text>
	</svg>" | write "kuha.svg"
        {} | exec "rsvg-convert" -o tiedosto "kuha.svg"
}

/* ohjaa kuvan luomista: käsittelee viestin, lataa kuvat, tekee kuhakuvan ja lähettää sen */
käsittele_kuhaviesti ketju viesti {
	/* esikäsitellään viesti */
	viesti ~= "kunhan" "kuha" "Kunhan" "Kuha" "&" "&amp;" "<" "&lt;"

	push "käsitellään viesti '" viesti "'\n"
	
	/* jaetaan viesti sanoihin */
	sanat := !(split viesti)
	montako := #sanat

	/* erillinen lista niistä sanoista, joista otetaan kuvia, oletuksena kaikki sanat */
	kuvasanat := sanat
	kuvamontako := montako

	/* jos kuvia tulisi liikaa, valitaan satunnaisesti vain osa */
	if test kuvamontako -gt 10; do
		kuvasanat = ()
		kuvamontako = 5

		/* hypitään satunnaisesti joidenkin kuvien yli */
		i := 0
		while test #kuvasanat -lt 5; do
			kuvasanat += sanat[i]
			i ++
			if random; do
				i ++
			done
		done
	done

	/* haetaan netistä kivoja kuvia */
	nettikuvat := ""
	laskuri := 0
	for sana in kuvasanat; do
		nettikuvat += !(hae_kuva_ja_tee_koodi sana laskuri kuvamontako)&" "
		laskuri ++
	done

	/* jaetaan sanat kahtia ja tehdään kuva */
	puoliväli := 'montako/2'
	tee_pilakuva nettikuvat sanat[:puoliväli]&" " sanat[puoliväli:]&" " "kuhaonmeemi.png"

	/* lähetetään kuva ketjuun */
	lähetä_kuva ketju "kuhaonmeemi.png"
}

/* lähettää viesti telegramissa annettuun ketjuun */
lähetä_viesti ketju viesti {
	{} | exec "curl" --silent tg_message -F "chat_id="..ketju -F "text="..viesti | {}
}

/* lähettää kuvan telegramissa annettuun ketjuun */
lähetä_kuva ketju kuva {
	{} | exec "curl" --silent tg_photo -F "chat_id="..ketju -F "photo=@"..kuva | {}
}

hae_tiedot kartta haut... {
	for noodi in kartta[1]; do
		for haku in haut; do
			if test noodi[0] -eq haku[0]; do
				haku[1] noodi[1]
			done
		done
	done
}

/* pääfunktio */
main {
	import "kuvahaku.röd"
	
	/* alustetaan botti */  
	token := !(cat "token.txt")[0]
	base_url := "https://api.telegram.org/bot"..token
	tg_update := base_url.."/getUpdates?offset="
	tg_message := base_url.."/sendMessage"
	tg_photo := base_url.."/sendPhoto"
	
	offset := 0
	
	aika_viimeksi := !(time)[0]
	tiheitä_viestejä := 0
	
	/* pääsilmukka */
	while true; do
		try do
			/* haetaan uusimmat päivitykset */
			koodi := !(wcat tg_update..offset)&" "
			parsittu := !(json koodi)[0]
			
			hae_tiedot parsittu ("result" { |päivitykset|
				for päivitys in päivitykset[1]; do
					ketju := 0
					teksti := ""
					hae_tiedot päivitys ("update_id" { |id|
						offset = id[1]
					}) ("message" { |viesti|
						hae_tiedot viesti ("chat" { |chat|
							hae_tiedot chat ("id" { |id|
								ketju = id[1]
							})
						}) ("text" { |text|
							teksti = text[1]
						}) ("caption" { |text|
							teksti = text[1]
						})
						if test tiheitä_viestejä -gt 3; do
							hae_tiedot viesti ("from" { |user|
								hae_tiedot user ("id" { |id|
									ketju = id[1]
								})	
							})
						done
					})
					if test teksti -matches ".*\\b[Kk][Uu][Hh][Aa].*"; do
						käsittele_kuhaviesti ketju teksti
						aika := !(time)[0]
						aikaa_kulunut := 'aika - aika_viimeksi'
						if test aikaa_kulunut -lt 10000; do
							tiheitä_viestejä ++
							if test tiheitä_viestejä -eq 4; do
								lähetä_viesti ketju "Rauhoittukaa, kuha on nyt hetken tauolla"
							done
						else
							tiheitä_viestejä = 0
						done
						aika_viimeksi = aika
					done
				done
			})
			
			{} | exec "sleep" 10 | {}
			
			offset ++
		done
	done
}