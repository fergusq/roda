case x {
	default := TRUE
	push { |ys...|; for y in ys if [ x =~ y ] do rval := default; default = FALSE; return rval; done; return FALSE }
	push { return default }
}

function execPlan(file, data) {
	error("file does not exist: `"..file.."'") unless fileExists(file)
	error("is directory: `"..file.."'") unless isFile(file)
	readLines(file) | lex() | parse() | interpret(data)
}

/*** LEXER ***/

record Token {}

record StringToken(line_num, str) : Token {
	line_num : integer = line_num
	val : string = str
}

record IdentifierToken(line_num, id) : Token {
	line_num : integer = line_num
	val : string = id
}

record KeywordToken(line_num, kw) : Token {
	line_num : integer = line_num
	val : string = kw
}

record EOFToken : Token {
	val : string = "<EOF>"
}

function lex() {
	line_num := 1
	splitMany(sep="\\|") | for row do
		sec_num := 1
		for section in row do
			if [ sec_num % 2 = 1 ] do
				push(new StringToken(line_num, section))
			else
				split(section, sep="(?<=[^\\wäöå])|(?=[^\\wäöå])") | validateTokens(line_num)
			done
			sec_num ++
		done
		line_num ++
		push(new StringToken(line_num, "\n"))
	done
	push(new EOFToken)
}

function validateTokens(line_num) {
	for token do
		case(token) *| pull(matches, somethingElse)
		push(new KeywordToken(line_num, token)) if matches("for|end|in|eq|block|ifdef|ifndef|if|ifn|include|\\.|\\[|\\]")
		push(new IdentifierToken(line_num, token)) if matches("[\\wäöå]+")
		if matches("\"") do
			l := []
			pull(token2)
			while [ token2 != "\"" ] do
				l += token2
				token2 = pull()
			done
			push(new StringToken(line_num, l&""))
		done
		{}() if matches("\\s*")
		error("illegal token: `"..token.."'") if somethingElse
	done
}

/*** PARSER ***/

function identifierToken(&var) {
	pull(token)
	error("expected identifier, got `"..token.val.."' (line "..token.line_num..")") unless [ token is IdentifierToken ]
	var := token
}

function identifier(&var) {
	identifierToken(token)
	var := token.val
}

function expect(kw) {
	pull(token)
	error("syntax error at token `"..token.val.."' (line "..token.line_num.."), expected `"..kw.."'") unless [ token is KeywordToken and token.val = kw ]
}

function expectMatch(kw_regex) {
	pull(token)
	error("syntax error at token `"..token.val.."' (line "..token.line_num.."), expected `"..kw.."'") unless [ token is KeywordToken and token.val =~ kw_regex ]
	push(token.val)
}

record Command {}

record PrintStringCommand(str) : Command {
	val : string = str
}

record VarCommand(chain) : Command {
	chain : list = chain
}

record ForCommand(var, expr, body) : Command {
	var : string = var
	expr : VarCommand = expr
	body : list<<Command>> = body
}

record IfDefCommand(expr, positive, body) : Command {
	expr : VarCommand = expr
	positive : boolean = positive
	body : list<<Command>> = body
}

record IfCommand(expr1, expr2, op, positive, body) : Command {
	expr1 : Command = expr1
	expr2 : Command = expr2
	op : string = op
	positive : boolean = positive
	body : list<<Command>> = body
}

record BlockCommand(var, body) : Command {
	var : string = var
	body : list<<Command>> = body
}

record IncludeCommand(file) : Command {
	file : string = file
}

function parse() {
	until [ tryPeek(token) and token is EOFToken ] do
		parseCommand()
	done
}

function parseCommand() {
	pull(token)
	if [ token is StringToken or token is IdentifierToken ] do
		parseExpression(token)
		return
	done
	if [ token is KeywordToken and token.val = "for" ] do
		parseFor()
		return
	done
	if [ token is KeywordToken and token.val =~ "ifn?def" ] do
		parseIfDef(token.val =~ "ifdef")
		return
	done
	if [ token is KeywordToken and token.val =~ "ifn?" ] do
		parseIf(token.val =~ "if")
		return
	done
	if [ token is KeywordToken and token.val = "block" ] do
		parseBlock()
		return
	done
	if [ token is KeywordToken and token.val = "include" ] do
		parseInclude()
		return
	done
	error("syntax error at token `"..token.val.."' (line "..token.line_num..")")
}

function parseExpression(token) {
	if [ token is StringToken ] do
		return new PrintStringCommand(token.val)
	done
	if [ token is IdentifierToken ] do
		return parseVar(token.val)
	done
	error("syntax error at token `"..token.val.."' (line "..token.line_num..")")
}

function parseBody() {
	body := new list<<Command>>
	until [ tryPeek(token) and token is KeywordToken and token.val = "end" ] do
		body += parseCommand()
	done
	expect("end")
	return body
}

function parseFor() {
	identifier(var)
	expect("in")
	identifier(chain_start)
	expr := parseVar(chain_start)
	body := parseBody()
	push(new ForCommand(var, expr, body))
}

function parseIfDef(positive) {
	identifier(chain_start)
	expr := parseVar(chain_start)
	body := parseBody()
	push(new IfDefCommand(expr, positive, body))
}

function parseIf(positive) {
	pull(token1)
	expr1 := parseExpression(token1)
	op := expectMatch("in|eq")
	pull(token2)
	expr2 := parseExpression(token2)
	body := parseBody()
	push(new IfCommand(expr1, expr2, op, positive, body))
}

function parseBlock() {
	identifier(var)
	body := parseBody()
	push(new BlockCommand(var, body))
}

function parseInclude() {
	pull(token)
	if [ token is StringToken ] do
		return new IncludeCommand(token.val)
	done
	error("syntax error at token `"..token.val.."' (line "..token.line_num..")")
}

function parseChain(&chain) {
	while [ peek() is KeywordToken and peek().val =~ "\\.|\\[" ] do
		if [ pull().val = "." ] do /* nimi esim. kartta.avain */
			identifier(id)
			chain += id
		else /* alaketju esim. kartta[a.b] */
			identifier(id)
			chain2 := [id]
			parseChain(chain2)
			expect("]")
			chain += chain2
		done
	done
}

function parseVar(var) {
	chain := new list
	chain += var
	parseChain(chain)
	push(new VarCommand(chain))
}

/*** INTERPRETER ***/

function interpret(scope) {
	for command do
		if [ command is PrintStringCommand ] do
			push(command.val); continue
		done
		if [ command is VarCommand ] do
			val := retrieveVar(scope, command.chain)
			if [ val is string ] do
				push(val)
				continue
			done
			if [ val is list<<Command>> ] do
				val() *| interpret(scope)
				continue
			done
			if [ val is list ] do
				/* listat käsitellään if in -rakennetta varten */
				push(val)
				continue
			done
			error("not clear what to do with this: `"..val.."'")
		done
		if [ command is BlockCommand ] do
			scope[command.var] = command.body
			continue
		done
		if [ command is IfDefCommand ] do
			obj := retrieveVar(scope, command.expr.chain[:-1])
			key := command.expr.chain[-1]
			if [ obj is list ] do
				continue if [ #obj > 0 ]
				key = parseInteger(key) /* TODO parempi virheenkäsittely */
			done
			if [ command.positive = obj[key]? ] do
				command.body() *| interpret(scope)
			done
			continue
		done
		if [ command is IfCommand ] do
			val1 := [push(command.expr1) *| interpret(scope)]
			val2 := [push(command.expr2) *| interpret(scope)]
			cond := FALSE
			if [ command.op = "eq" ] do
				cond = [ val1 = val2 ]()
			done
			if [ command.op = "in" ] do
				unless [ #val1 = 1 and #val2 = 1 and val2[0] is list ] do
					error("illegal use of if-in: the operands should single values and the second operand should be a list")
				done
				cond = [ val1[0] in val2[0] ]()
			done
			if [ command.positive = cond ] do
				command.body() *| interpret(scope)
			done
			continue
		done
		if [ command is ForCommand ] do
			vals := retrieveVar(scope, command.expr.chain)
			unless [ vals is list ] do
				error("illegal use of for: `"..vals.."' is not iterable")
			done
			for val in vals do
				scope[command.var] = val
				command.body() *| interpret(scope)
			done
			continue
		done
		if [ command is IncludeCommand ] do
			execPlan(command.file, scope)
		done
	done
}

function retrieveVar(scope, chain) {
	current_val = scope
	while [ #chain > 0 ] do
		key := chain[0]
		if [ key is list ] do
			key = retrieveVar(scope, key)
			unless [ key is string ] do
				error("not clear what to do with this: `"..key.."'")
			done
		done
		if [ current_val is list ] do
			case(key) *| pull(matches, somethingElse)
			if matches("[0-9]+") do
				key = parseInteger(key)
			done
			if matches("sorted_by_\\w+") do
				/* sort-funktiota ei vielä ole Rödassa, joten tämä ei toimi sitten */
				current_val = [current_val() *| sort(key={ |e|; return e[key[10:]]})]
				shift chain
				continue
			done
			if somethingElse do
				error("unknown index or method: `"..key.."'")
			done
		done
		if [ current_val[key]? ] do
			current_val = current_val[key]
			shift chain
		else /* oikeastaan onko tässä järkeä, sillä Röda-tulkin oletusvirhe on lähes sama... */
			error("variable or key not found: `"..key.."'")
		done
	done
	return current_val
}
