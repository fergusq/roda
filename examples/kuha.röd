hae_kuva_ja_tee_koodi sana, mones, montako {
	hae_kuva sana, mones
	x_koordinaatti := mones*500//montako
	leveys := 500//montako
	push "<image xlink:href='"..mones.."' preserveAspectRatio='none' x='"..x_koordinaatti.."' y='50' width='"..leveys.."' height='150' style='opacity:0.4;'/>"
}

/* tekee svg-kuvan */
tee_pilakuva nettikuvat, ylä, ala, tiedosto {
	push "<?xml version='1.0' encoding='UTF-8'?>
	<svg
	xmlns='http://www.w3.org/2000/svg'
	xmlns:xlink='http://www.w3.org/1999/xlink' version='1.1'
	viewBox='0 0 500 244'>
	<style type=\"text/css\">@font-face{font-family:'imppa';src:url('Impact.ttf');}text{font-family:'serif';font-size:35px;fill:white;stroke:black;stroke-width:2px;text-anchor:middle}</style>
	<image xlink:href='kuha.jpg' x='0' y='0' width='500' height='244'/>
	"..nettikuvat.."
	<text x='50%' y='14%'>"..ylä.."</text>
	<text x='50%' y='98%'>"..ala.."</text>
	</svg>" | write "kuha.svg"
	{} | exec "rsvg-convert", "-o", tiedosto, "kuha.svg"
}

/* ohjaa kuvan luomista: käsittelee viestin, lataa kuvat, tekee kuhakuvan ja lähettää sen */
käsittele_kuhaviesti botti ketju viesti {
	/* esikäsitellään viesti */
	viesti ~= "kunhan", "kuha", "Kunhan", "Kuha", "&", "&amp;", "<", "&lt;"

	push "käsitellään viesti '", viesti, "'\n"
	botti.send_action ketju, "upload_photo"

	/* jaetaan viesti sanoihin */
	sanat := [split(viesti)]
	montako := #sanat

	/* erillinen lista niistä sanoista, joista otetaan kuvia, oletuksena kaikki sanat */
	kuvasanat := sanat
	kuvamontako := montako

	/* jos kuvia tulisi liikaa, valitaan satunnaisesti vain osa */
	if [ kuvamontako > 10 ]; do
		kuvasanat = []
		kuvamontako = 5

		/* hypitään satunnaisesti joidenkin kuvien yli */
		i := 0
		while [ #kuvasanat < 5 ]; do
			kuvasanat += sanat[i]
			i ++
			if random; do
				i ++
			done
		done
	done

	/* haetaan netistä kivoja kuvia */
	nettikuvat := ""
	laskuri := 0
	for sana in kuvasanat; do
		nettikuvat .= [hae_kuva_ja_tee_koodi(sana, laskuri, kuvamontako)]&" "
		laskuri ++
	done

	/* jaetaan sanat kahtia ja tehdään kuva */
	puoliväli := montako//2
	tee_pilakuva nettikuvat, sanat[:puoliväli]&" ", sanat[puoliväli:]&" ", "kuhaonmeemi.png"

	/* lähetetään kuva ketjuun */
	botti.send_photo ketju, "kuhaonmeemi.png"
}

{
	import "kuvahaku.röd"
}

/* pääfunktio */
main {
	import "telegram.röd"
	token := [cat("token.txt")][0]
	bot := tg_init(token)
	bot.on_message = { |ketju, teksti|
		if [ teksti =~ ".*\\b[Kk][Uu][Hh][Aa].*" ]; do
			käsittele_kuhaviesti bot, ketju, teksti
		done
	}
	print "Started."
	while true; do
		try do
			bot.update
		catch e
			print "VIRHE: "..e.message
		done
	done
}

